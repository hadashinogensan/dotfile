set nocompatible

set runtimepath^=~/.vim/dein/repos/github.com/Shougo/dein.vim

call dein#begin(expand('~/.vim/dein'))

  " dein自身をdeinで管理する
  call dein#add('Shougo/dein.vim')
  " vim上でコマンドを実行できるようにするプラグイン
  call dein#add('Shougo/vimproc.vim', {'build' : 'make'})
  " You can specify revision/branch/tag.
  call dein#add('Shougo/vimshell', { 'rev': '3787e5' })
  " Emmet
  call dein#add('mattn/emmet-vim')
  " 補完
  call dein#add('Shougo/neocomplete.vim')
  " スニペット
  call dein#add('Shougo/neosnippet.vim')
  call dein#add('Shougo/neosnippet-snippets')
  call dein#add('honza/vim-snippets')
  " ファイラー的な
  call dein#add('Shougo/unite.vim')
  " Uniteで関数一覧などのアウトラインを表示する
  call dein#add('Shougo/unite-outline')
  " Unite からgitを利用できるように
  call dein#add('kmnk/vim-unite-giti.git')
  " Unite で最近開いたファイルを見れる
  call dein#add('Shougo/neomru.vim')
  " ifやdoを入力したら自動的にendを挿入してくれる
  call dein#add('tpope/vim-endwise')
  " vimでGitを扱う
  call dein#add('tpope/vim-fugitive')
  " Gitの変更を見やすくする
  call dein#add('airblade/vim-gitgutter')
  " :RTとかで対応するviewとかに移動できる
  call dein#add('tpope/vim-rails')
  " テキストを特定の文字で囲んだりする
  call dein#add('tpope/vim-surround')
  " trueとfalseをきりかえたり
  call dein#add('AndrewRadev/switch.vim')
  " 視覚的にreplaceできるやつ
  call dein#add('osyo-manga/vim-over')
  " 選択範囲を外側に広げて検索できる
  call dein#add('terryma/vim-expand-region')
  "行単位でdiffをみるためのプラグイン
  call dein#add('AndrewRadev/linediff.vim')
  " シンタックスエラーを見つけてくれるやつ
  call dein#add('scrooloose/syntastic.git')
  " 画面上の見えている範囲の移動を早くするためのプラグイン
  call dein#add('Lokaltog/vim-easymotion')
  " = の位置とかそろえられるやつ！
  call dein#add('junegunn/vim-easy-align')
  " fの検索機能を強化する
  call dein#add('rhysd/clever-f.vim')
  " ステータスラインをかっこよく
  call dein#add('itchyny/lightline.vim')
  " コーディングのログを収集してくれるサービス
  call dein#add('wakatime/vim-wakatime')
  " jsonの表示がおかしいのを治す
  call dein#add('elzr/vim-json')
  " 編集しているファイルをGitHubで開く
  call dein#add('tyru/open-browser.vim')
  call dein#add('tyru/open-browser-github.vim')

  " ==================================================
  " テキストオブジェクト
  " ==================================================
  " テキストオブジェクト系のプラグインが依存しているプラグイン
  call dein#add('kana/vim-textobj-user')
  " [ l ] カーソル行
  call dein#add('kana/vim-textobj-line')
  " [ u ] Url
  call dein#add('mattn/vim-textobj-url')
  " [ , ] 引数
  call dein#add("sgur/vim-textobj-parameter")

  " ==================================================
  " シンタックスハイライト
  " ==================================================
  " Coffee
  call dein#add('kchmck/vim-coffee-script', {'autoload':{'filetypes':['coffee']}})
  " Scala
  call dein#add('derekwyatt/vim-scala')
  " React
  call dein#add('pangloss/vim-javascript')
  call dein#add('mxw/vim-jsx')
  " haml
  call dein#add('tpope/vim-haml')

  " 未インンストールのプラグインがあったらインストールする
  if dein#check_install()
    call dein#install()
  endif

call dein#end()

filetype plugin on
filetype plugin indent on
filetype indent on

" ==================================================
" Unite
" ==================================================

" Prefix
nnoremap [unite] <nop>
nmap <space>u [unite]

let g:unite_enable_start_insert = 1
let g:unite_enable_split_vertically = 0
let g:unite_winwidth = 40
let g:unite_cursor_line_highlight = 7

" ファイルはデフォルトでtabで開くように
call unite#custom#default_action('file', 'tabopen')
" ディレクトリはデフォルトで再帰的にファイルを表示するように
call unite#custom#default_action('directory', 'file')
" ドットファイルが表示されるように
call unite#custom#source('file', 'matchers', "matcher_default")

" 画像ファイルを検索対象から外す
let s:unite_ignore_extensions = join(['gif', 'jpg', 'jpeg', 'png', 'webp', 'woff', 'svg', 'psd', 'ico', 'DS_Store', 'keep'],'\|')
let s:unite_ignore_patterns='\.\('.s:unite_ignore_extensions.'\)$'
call unite#custom#source('file_rec/async', 'ignore_pattern', s:unite_ignore_patterns)
call unite#custom#source('file_rec/git', 'ignore_pattern', s:unite_ignore_patterns)

" Unite
nnoremap <silent> [unite]f :<C-u>UniteWithBufferDir -buffer-name=files file<CR>
nnoremap <silent> [unite]p :<C-u>UniteWithProjectDir -buffer-name=files file<CR>
nnoremap <silent> [unite]o :<C-u>Unite outline<CR>
nnoremap <silent> [unite]m :<C-u>Unite file_mru buffer<CR>
nnoremap <silent> [unite]b :<C-u>Unite buffer<CR>

" Grep
nnoremap <silent> [unite]g :<C-u>Unite grep -buffer-name=grep -default-action=tabopen<CR>
if executable('ag')
  let g:unite_source_grep_command = 'ag'
  let g:unite_source_grep_default_opts = '--nogroup --nocolor --column'
endif

augroup UniteSelectAction
  autocmd!
  autocmd FileType unite inoremap <silent> <buffer> <expr> <C-k> unite#do_action('rec/async')
  autocmd FileType unite nnoremap <silent> <buffer> <expr> <C-k> unite#do_action('rec/async')
  autocmd FileType unite inoremap <silent> <buffer> <expr> <C-g> unite#do_action('grep')
  autocmd FileType unite nnoremap <silent> <buffer> <expr> <C-g> unite#do_action('grep')
augroup END


" ==================================================
" neocomplete
" ==================================================

" neocomplcacheを自動起動する
let g:neocomplete#enable_at_startup = 1
" 大文字が入力されるまで大文字小文字を区別しない
let g:neocomplete#enable_smart_case = 1
" シンタックスをキャッシュする際の最小文字長
let g:neocomplete#sources#syntax#min_keyword_length = 3
" バッファ名のパターンを指定する
let g:neocomplete#lock_buffer_name_pattern = '\*ku\*'
" ポップアップで表示される候補の数
let g:neocomplete#max_list = 20
" キャッシュしないファイル名
let g:neocomplete#sources#buffer#disabled_pattern = '\.log\|\.log\.\|\.jax'
" 自動補完を行わないバッファ名
let g:neocomplete#lock_buffer_name_pattern = '\.log\|\.log\.\|.*quickrun.*\|.jax'

" rubyのオムニ補完
if !exists('g:neocomplete#sources#omni#input_patterns')
  let g:neocomplete#sources#omni#input_patterns = {}
endif
let g:neocomplete#sources#omni#input_patterns.ruby = '[^. *\t]\.\h\w*\|\h\w*::'

" 補完をキャンセルし元に戻す
inoremap <expr><C-g> neocomplete#undo_completion()

" 補完候補の共通部分のみを補完する
inoremap <expr><C-l> neocomplete#complete_common_string()

" Enterで候補を選択し、改行もされないようにする
function! s:my_crinsert()
    return pumvisible() ? neocomplete#close_popup() : "\<Cr>"
endfunction
inoremap <silent> <CR> <C-R>=<SID>my_crinsert()<CR>

" <C-h>, <BS>補完を禁止
inoremap <expr><C-h> neocomplete#smart_close_popup()."\<C-h>"
inoremap <expr><BS> neocomplete#smart_close_popup()."\<C-h>"

" ==================================================
" neosnippet
" ==================================================
let g:neosnippet#snippets_directory='~/.vim/dein/repos/github.com/honza/vim-snippets/snippets,~/dotfiles/vim/snippets'

inoremap <expr><S-TAB>  pumvisible() ? "\<C-p>" : "\<S-TAB>"

" Plugin key-mappings.
imap <C-k> <Plug>(neosnippet_expand_or_jump)
smap <C-k> <Plug>(neosnippet_expand_or_jump)

" SuperTab like snippets behavior.
imap <expr><TAB> neosnippet#jumpable() ? "\<Plug>(neosnippet_expand_or_jump)" : pumvisible() ? "\<C-n>" : "\<TAB>"
smap <expr><TAB> neosnippet#jumpable() ? "\<Plug>(neosnippet_expand_or_jump)" : "\<TAB>"

" For snippet_complete marker.
if has('conceal')
  set conceallevel=2 concealcursor=i
endif

" ==================================================
" vim-fugitive
" ==================================================

nnoremap [git] <Nop>
vnoremap [git] <Nop>
nmap <Space>g [git]
vmap <Space>g [git]

nnoremap <silent> [git]df :Gdiff<CR>
nnoremap <silent> [git]ad :Gwrite<CR>
nnoremap <silent> [git]cv :Gcommit -v<CR>
nnoremap <silent> [git]cam :Gcommit --amend<CR>
nnoremap <silent> [git]bl :Gblame<CR>
nnoremap <silent> [git]st :Gstatus<CR>

" ==================================================
" open-browser-github
" ==================================================

nnoremap <silent> [git]g :OpenGithubFile<CR>
vnoremap <silent> [git]g :OpenGithubFile<CR>

" ==================================================
" switch.vim
" ==================================================

" Prefix
nnoremap [switch] <Nop>
nmap <Space>t [switch]

inoremap [switch] :Switch<CR>
nnoremap [switch] :Switch<CR>
vnoremap [switch] :Switch<CR>

augroup SwitchConfig
  autocmd!
  autocmd FileType *  let b:switch_custom_definitions = [
        \   ['true', 'false'],
        \ ]

  autocmd FileType ruby,eruby,haml let b:switch_custom_definitions = [
        \   ['if', 'unless'],
        \   ['.blank?', '.present?'],
        \   ['include', 'extend'],
        \   ['class', 'module'],
        \   ['attr_accessor', 'attr_reader', 'attr_writer'],
        \ ]
augroup END


" ==================================================
" vim-over
" ==================================================

nnoremap [over] <Nop>
nmap <Space>s [over]
vmap <Space>s [over]

nnoremap <silent> [over]s :OverCommandLine<CR>s/
vnoremap <silent> [over]s :OverCommandLine<CR>s/
nnoremap <silent> [over]% :OverCommandLine<CR>%s/

" ==================================================
" vim-rails
" ==================================================

nnoremap [rails] <Nop>
nmap <Space>r [rails]

nnoremap <silent> gp :bp<CR>

" ==================================================
" vim-easymotion
" ==================================================

"Disable default mappings
let g:EasyMotion_do_mapping = 0
" 大文字小文字を区別しない
let g:EasyMotion_smartcase = 1

nmap <Space>e <Plug>(easymotion-s2)

" ==================================================
" vim-easy-align
" ==================================================

xmap <Space>e <Plug>(EasyAlign)

" ==================================================
" vim-lightline
" ==================================================
set laststatus=2
set t_Co=256

let g:lightline = {
  \   'mode_map': {'c': 'NORMAL'},
  \   'active': {
  \     'right': [
  \        [ 'syntastic', 'lineinfo'],
  \        [ 'percent' ],
  \        [ 'fileformat', 'fileencoding', 'filetype' ]
  \      ],
  \     'left':  [ [ 'mode'], ['fugitive'] ]
  \   },
  \   'component_function': {
  \     'modified': 'LightLineModified',
  \     'readonly': 'LightLineReadonly',
  \     'fugitive': 'LightLineFugitive',
  \     'filename': 'LightLineFilename',
  \     'fileformat': 'LightLineFileformat',
  \     'filetype': 'LightLineFiletype',
  \     'mode': 'LightLineMode',
  \   },
  \   'component_expand': {
  \     'syntastic': 'SyntasticStatuslineFlag'
  \   },
  \   'component_type': {
  \     'syntastic': 'error'
  \   }
  \ }

function! LightLineModified()
  return &ft =~ 'help\|vimfiler\|gundo' ? '' : &modified ? '+' : &modifiable ? '' : '-'
endfunction

function! LightLineReadonly()
  return &ft !~? 'help\|vimfiler\|gundo' && &readonly ? '🔒 ' : ''
endfunction

function! LightLineFilename()
  return ('' != LightLineReadonly() ? LightLineReadonly() . ' ' : '') .
        \ (&ft == 'vimfiler' ? vimfiler#get_status_string() :
        \  &ft == 'unite' ? unite#get_status_string() :
        \  &ft == 'vimshell' ? vimshell#get_status_string() :
        \ '' != expand('%:t') ? expand('%:t') : '[No Name]') .
        \ ('' != LightLineModified() ? ' ' . LightLineModified() : '')
endfunction

function! LightLineFugitive()
  try
    if &ft !~? 'vimfiler\|gundo' && exists('*fugitive#head')
      return fugitive#head()
    endif
  catch
  endtry
  return ''
endfunction

function! LightLineFileformat()
  return winwidth(0) > 70 ? &fileformat : ''
endfunction

function! LightLineFiletype()
  return winwidth(0) > 70 ? (strlen(&filetype) ? &filetype : 'no ft') : ''
endfunction

function! LightLineFileencoding()
  return winwidth(0) > 70 ? (strlen(&fenc) ? &fenc : &enc) : ''
endfunction

function! LightLineMode()
  return winwidth(0) > 60 ? lightline#mode() : ''
endfunction

" ==================================================
" syntastic
" ==================================================
"  mode に active を指定すると保存するたびに、
"          passiveを指定すると :SyntasticCheck を実行時に検出されます
let g:syntastic_ruby_checkers = [ 'rubocop' ]
let g:syntastic_javascript_checkers = [ 'eslint' ]
let g:syntastic_scss_checkers = ['scss_lint']

let g:syntastic_enable_perl_checker = 1
let g:syntastic_perl_checkers = ['perl', 'podchecker']

augroup AutoSyntastic
  autocmd!
  autocmd BufWritePost * call s:syntastic()
augroup END

function! s:syntastic()
  SyntasticCheck
  call lightline#update()
endfunction

" ==================================================
" Emmet
" ==================================================
let g:user_emmet_expandabbr_key='<c-y>'

" ==================================================
" vim-json
" ==================================================
let g:vim_json_syntax_conceal = 0

" ==================================================
" clever-f.vim
" ==================================================
" 大文字を入力した時だけ大文字小文字を区別するように
let g:clever_f_smart_case = 1
" 現在行のみを検索対象に
let g:clever_f_across_no_line = 1
" fとFの移動方向を固定する
let g:clever_f_fix_key_direction = 1

" ==================================================
" vim-expand-region
" ==================================================
vmap v <Plug>(expand_region_expand)
vmap <C-v> <Plug>(expand_region_shrink)

